buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.apache.httpcomponents:httpclient:4.5.2"
        classpath group: 'org.ccil.cowan.tagsoup', name: 'tagsoup', version: '1.2.1'
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.0.43'
    id "de.undercouch.download" version "3.1.0"
}


import de.undercouch.gradle.tasks.download.Download
import org.ccil.cowan.tagsoup.Parser

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'idea'

intellij {
    version ideaVersion
    type ideaType
    plugins = ['php', 'java-i18n', 'properties', 'CSS']
    updateSinceUntilBuild false
    sameSinceUntilBuild false
    sandboxDirectory "${project.rootDir}/.sandbox"
    pluginName name
}

idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

sourceSets {
    main {
        kotlin.srcDirs += 'src'
        resources.srcDir 'resources'
    }
    test {
        kotlin.srcDir 'tests'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

repositories {
    mavenCentral()
}

// Big thanks to https://github.com/orocrm/oro-phpstorm-plugin
def phpPluginFiles(String version) {
    fileTree(dir: "${project.intellij.ideaDirectory}/plugins/php-$version/php/lib/", include: '*.jar')
}

def phpGetPluginUrl(String version) {
    def htmlText = new URL('http://plugins.jetbrains.com/plugin/6610?&showAllUpdates=true').getText()
    def htmlPage = new XmlSlurper(new Parser()).parseText(htmlText)
    def htmlVersions = htmlPage.'**'.find {
            it.name() == 'table' &&
            it.@class == 'version_table'
        }.tr[1..-1]

    if (version == "LATEST") {
        return htmlVersions[0].td[-1].a.@href
    } else {
        return htmlVersions.find { it.td[0].text() == version }.td[-1].a.@href
    }
}

afterEvaluate {
    it.tasks.findByName("compileKotlin").dependsOn("downloadAndUnzipPhpPlugin")
    it.intellij.intellijFiles.addAll(phpPluginFiles(phpPluginVersion).getFiles())

    project.tasks.create(name: "downloadPhpPlugin", type: Download) {
        def output = new File("${project.intellij.ideaDirectory}/plugins/php-${phpPluginVersion}.zip")

        src "https://plugins.jetbrains.com${phpGetPluginUrl(phpPluginVersion)}"
        dest output
        overwrite false
    }

    project.tasks.create(name: "downloadAndUnzipPhpPlugin", dependsOn: "downloadPhpPlugin", type: Copy) {
        from zipTree(downloadPhpPlugin.dest)
        into "${project.intellij.ideaDirectory}/plugins/php-$phpPluginVersion"
    }

    dependencies {
        compile phpPluginFiles(phpPluginVersion)
    }
}



